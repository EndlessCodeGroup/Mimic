// Bukkit MimicAPI implementation build config

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "ru.endlesscode.bukkitgradle" version "0.8.0"
}

def minorVersion = 0

version = "${apiProject.version}.$minorVersion"
description = "API for integration with Bukkit RPG plugins"

ext.pluginName = "BukkitMimic"

bukkit {
    version = "1.12.2"

    meta {
        name = pluginName
        main = "$group.$name"
        authors = ["OsipXD", "EndlessCodeGroup"]
        url = "https://github.com/EndlessCodeGroup/BukkitMimic"
    }

    run {
        core = "paper"
        eula = true
    }
}

repositories {
    maven {
        name = "ec-repo"
        url = "https://raw.github.com/EndlessCodeGroup/mvn-repo/master/"
    }

    maven {
        name = "aikar-repo"
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    compileOnly bukkit()
    implementation libraries.acf
    applyDeps(bukkitRpgPlugins) { compileOnly it }
    applyDeps(testingDeps) { testImplementation it }
}

compileJava {
    options.compilerArgs += ["-parameters", "-Xlint"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    relocate "co.aikar.commands", "${group}.shade.acf"
}

build.dependsOn shadowJar

// Exclude Main plugin class from test coverage
jacocoTestReport.doFirst {
    classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it, exclude: "**/BukkitMimic.**")
    })
}
