plugins {
    id 'ru.endlesscode.bukkitgradle' version '0.6.7'
}

apply from: rootProject.file('gradle/api.gradle')
apply from: rootProject.file('gradle/dependencies.gradle')

bukkit {
    version = '1.11.2'
    meta {
        main = 'ru.endlesscode.mimic.bukkit.BukkitMimic'
        authors = ['OsipXD', 'EndlessCodeGroup']
    }

    run {
        eula = true
        dir = '../DevServer'
    }
}

repositories {
    maven {
        name 'ec-repo'
        url 'https://raw.github.com/EndlessCodeGroup/mvn-repo/master/'
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    compileOnly group: 'com.sucy', name: 'SkillAPI', version: '3.102'
    compileOnly group: 'com.herocraftonline.heroes', name: 'Heroes', version: '1.5.5.6'
    applyDeps(testingDeps) { testImplementation it }
}

// Exclude Main plugin class from test coverage
jacocoTestReport.doFirst {
    classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it, exclude: '**/BukkitMimic.**')
    })
}
