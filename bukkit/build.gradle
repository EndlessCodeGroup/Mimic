// Bukkit MimicAPI implementation build config

plugins {
    id "ru.endlesscode.bukkitgradle" version "0.7.0"
}

def minorVersion = 0

version = "${apiProject.version}.$minorVersion"
description = "API for integration with Bukkit RPG plugins"

ext.pluginName = "BukkitMimic"

archivesBaseName = pluginName

bukkit {
    version = "1.12.2"

    meta {
        name = pluginName
        main = "$group.$name"
        authors = ["OsipXD", "EndlessCodeGroup"]
        url = "https://github.com/EndlessCodeGroup/BukkitMimic"
    }

    run {
        eula = true
        dir = "../DevServer"
    }
}

repositories {
    maven {
        name "ec-repo"
        url "https://raw.github.com/EndlessCodeGroup/mvn-repo/master/"
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    compileOnly bukkit()
    applyDeps(bukkitRpgPlugins) { compileOnly it }
    applyDeps(testingDeps) { testImplementation it }
}

// Exclude Main plugin class from test coverage
jacocoTestReport.doFirst {
    classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it, exclude: "**/BukkitMimic.**")
    })
}
