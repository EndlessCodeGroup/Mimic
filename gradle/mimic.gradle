// Shared Gradle configuration for Mimic projects

apply plugin: 'java'
apply plugin: 'idea'

group = 'ru.endlesscode'

// Java version
sourceCompatibility = targetCompatibility = '1.8'

afterEvaluate {
    // Set it again in case ForgeGradle or BukkitGradle resets it
    sourceCompatibility = targetCompatibility = '1.8'
}

// Adding Java 1.8 tags support to javadoc
javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.tags = ['apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:', 'implNote:a:Implementation Note:']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Apply and configure Checkstyle
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.1'
    configFile = api.file('config/checkstyle.xml')
    configProperties = [
            'basedir': project.projectDir,
            'severity': 'warning'
    ]
}

// Disable checkstyle by default (won't run unless 'checkstyle' is explicitly invoked)
task checkstyle(dependsOn: tasks.withType(Checkstyle))

gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
    if (!graph.allTasks.contains(tasks.checkstyle)) {
        graph.allTasks.findAll { it.name.startsWith('checkstyle') }.each { it.enabled = false }
    }
}

// Include LICENSE in final JAR
processResources {
    from 'LICENSE.txt'
}

// Gradle version used for generating the Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = api.gradleVersion
}

// JaCoCo settings
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

// Common repositories and dependencies
repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
    testCompileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
}